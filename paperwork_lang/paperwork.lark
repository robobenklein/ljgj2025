
%import common.WS
%import common.INT
%import common.NEWLINE
%import common.WS_INLINE
_LINE_END: WS_INLINE? NEWLINE
_WS: WS_INLINE

_seperated{x, sep}: x (sep x)*  // Define a sequence of 'x sep x sep x ...'

start: code_block

code_block: _seperated{line, _LINE_END}

?line: _instruction | label | comment | _WS

label: "#" label_name
label_name: /[\w]+/
comment: /# [^\n]*/

_instruction: ins_move | ins_take | ins_drop

// the actor moves to a specific location of a type
ins_move: "MOVE"i _WS location_type _WS location_identifier

// the actor takes something from where they are:
// could be anything, or could be specific
ins_take: "TAKE"i (_WS parcel_type (_WS parcel_identifier)?)?

// the actor puts down a moveable in their posession
ins_drop: "DROP"i (_WS parcel_type (_WS parcel_identifier)?)?

location_type: /desk|floor/i
location_identifier: /[A-Za-z]+/i
parcel_type: /any|doc|box/i
parcel_identifier: INT

%ignore WS_INLINE
